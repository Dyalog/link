 r←Setup(folder name);udebug;z
 r←'' ⍝ Run will abort if empty
 z←⎕SE.Link.Break name
 ⎕EX name
 :If 0≠⎕NC'⎕SE.Link.Links'
 :AndIf 0≠CountLinks
     Log'Please break all links and try again.'
     ⎕←⎕SE.UCMD']Link.Status'
     ⎕←'      ]Link.Break -all=*    ⍝ to break all links'
     :Return
 :EndIf
 :If 0≠⎕NC name
     ⎕EX name
 :EndIf
 :If ~0∊⍴folder  ⍝ specific folder
     folder←∊1 ⎕NPARTS folder,(0=≢folder)/(739⌶0),'/linktest'
     :If ⎕NEXISTS folder
         Log folder,' exists. Clean it out? [Y|n] '
         :If 'yYjJ1 '∊⍨⊃⍞~' '
             3 ⎕NDELETE Retry⊢folder
         :Else
             Log'Directory must be non-existent.'
             :Return
         :EndIf
     :EndIf
 :Else  ⍝ generate a new directory - avoids prompting for deletion
     folder←CreateTempDir 1
 :EndIf

 ⎕SE.Link.USERCONFIGFILE←folder,'-config/.linkconfig' ⍝ Do not use the users normal config file!
 (⊂'{Settings:{}}') ⎕NPUT ⎕SE.Link.USERCONFIGFILE
 SetupSlave ⍝ Set up "SLAVE" namespace for handling file operations

 r←folder
