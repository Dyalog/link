 {r}←Expunge names;container;file;i;link;links;name;nc;oldfile;parent;ref;scal;shape;⎕IO;⎕ML;leaf;z;ns
⍝ Use in place of ⎕EX for linked names - will delete source files where relevant in addition to ⎕EX
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :Hold '⎕SE.Link.Links'
         container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬
         :If 0∊⍴links←U.GetLinks ⋄ r←container.⎕EX names ⋄ :Return ⋄ :EndIf
         scal←1=≡,names ⋄ shape←⍴names←,¨⊆names ⋄ r←(≢names←,names)⍴0
         :For i :In ⍳≢names
             :If 0=≢i⊃names
                 nc←¯1
             :Else
                 (ns name)←container U.SplitNs i⊃names
                 ns←⍎ns ⍝ resolve e.g. #.linkdemo.##
                 nc←container U.NameClass name←(⍕ns){(1↑⍵)∊'#⎕':⍵ ⋄ ⍺,'.',⍵}name
             :EndIf
             :If nc∊0 ¯1
                 r[i]←0
                 :Continue
             :EndIf
             :If ¯9.1=nc ⍝ namespace linked to directory
             :AndIf (⊂U.NormNs name)∊links.ns  ⍝ is a link root
                 U.ErrorIfRestored links  ⍝ error only in linked namespaces
                 {}Break name  ⍝ github issue #119: tell user about problems
                 r[i]←⎕EX name
             :Else
                 :Hold '⎕SE.Link.Notify'
                     (parent leaf)←U.SplitNs name
                     :If ~0∊⍴link←links U.LookupRef parent←⍎parent
                         U.ErrorIfRestored links ⍝ error only in linked namespaces
                         :If link.singlefile
                             U.Error'You may not Expunge an object within a linked namespace, class or interface'
                         :EndIf
                         (file oldfile)←2↑link(0 U.DetermineFileName)(⍕parent)leaf leaf nc ⍬ ⍝ U.NormNs parent
                         :If (parent.⎕NC leaf)∊3 4
                             z←Stop name ⍬
                             z←Trace name ⍬
                         :EndIf
                         r[i]←parent.⎕EX leaf  ⍝ do it before the watcher kicks in
                         :If (⊂link.watch)∊'ns' 'both'
                             :If 0<≢oldfile  ⍝ tied to file
                                 2 ⎕NDELETE oldfile
                             :ElseIf ⎕NEXISTS file ⍝ guess file name
                                 2 ⎕NDELETE file
                             :EndIf
                         :EndIf
                     :Else
                         :If 9=⎕NC name
                         :AndIf ~0∊⍴link←links U.LookupRef⍎name ⍝ Single linked file
                             U.Error'You may not Expunge a linked namespace, class or interface'
                         :EndIf
                         r[i]←parent.⎕EX name
                     :EndIf
                 :EndHold ⍝ Notify
             :EndIf
         :EndFor
         r←⊃⍣scal⊢shape⍴r
     :EndHold
 :Else
     U.Resignal 1
 :EndTrap
