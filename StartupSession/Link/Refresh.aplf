 msg←{opts}Refresh nss;inFail;ko;link;links;ns;nsref;ok;outFail;rsi;source;xsi;⎕IO;⎕ML;setconfig
 ⎕IO ⎕ML←1 1
 preProcessOpts ⍝ Make sure opts is a namespace
 (msg setconfig)←Config.GetConfig opts
 →(0≠≢msg)⍴0
 :If 2=DEBUG←{6::0 ⋄ ⍵.Debug.debug}setconfig
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :Hold '⎕SE.Link.Links'
         :If 0∊⍴links←U.ErrorIfRestored U.GetLinks ⋄ msg←'No active links' ⋄ :Return ⋄ :EndIf
         opts←U.DefaultOpts opts
         :If ''≡0⍴nss ⋄ nss←⊂nss ⋄ :EndIf  ⍝ ⊆ would enclose vector of namespaces
         ok←ko←⍬
         (rsi xsi)←(⎕RSI ⎕XSI)
         :For ns :In nss
             ns←⊃⌽rsi xsi U.ContainerNs ns
             :If ~0∊⍴nsref←(0 U.GetRefTo)ns
             :AndIf ~0∊⍴link←links U.LookupRef nsref
                 :If opts.source≡'auto' ⋄ source←link.source   ⍝ use actual source used at Create time
                 :Else ⋄ source←opts.source  ⍝ use specified source
                 :EndIf
                 0 Watcher.Pause link
                 :If 'ns'≡source
                     ok,←⊂'Exported: ',ns,' → ',U.FmtPath link.dir
                     :If 0≠≢outFail←2⊃link U.WriteFiles link.ns link.dir 1 ⍝  always overwrite
                         ko,←(⊂(⍕≢outFail),' export(s) failed:'),outFail
                     :EndIf
                 :ElseIf 'dir'≡source
                     ok,←⊂'Imported: ',ns,' ← ',U.FmtPath link.dir
                     :If 0≠≢inFail←2⊃link U.FixFiles(⍎link.ns)link.dir 1  ⍝ always overwrite
                         ko,←(⊂(⍕≢inFail),' import(s) failed:'),inFail
                     :EndIf
                 :EndIf
                 1 Watcher.Pause link  ⍝ TODO : WriteFiles/FixFiles unnecessary when un-pausing crawler
             :Else
                 ko,←⊂'Not linked: ',ns
             :EndIf
         :EndFor
     :EndHold
     msg←1↓U.FmtLines ok,((×≢ko)/⊂'ERRORS ENCOUNTERED:'),ko
 :Else
     U.Resignal 1
 :EndTrap
