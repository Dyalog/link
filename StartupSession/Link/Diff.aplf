 diff←{opts}Diff ns;container;link;links;nsref;⎕IO;⎕ML
 ⎕IO ⎕ML←1 1
 preProcessOpts
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :Hold '⎕SE.Link.Links'
         :If 0∊⍴links←U.GetLinks ⋄ U.Error'Not linked: ',⍕ns ⋄ :EndIf
         opts←U.DefaultOpts opts
         (container ns)←(⎕RSI ⎕XSI)U.ContainerNs ns
         :If ~0∊⍴nsref←(0 U.GetRefTo)ns
         :AndIf ~0∊⍴link←links U.LookupRef nsref
             0 Watcher.Pause link  ⍝ FSW cannot kick in because of the :Hold
             link←⎕NS link ⋄ link.arrays←opts.arrays
             diff←U.Diff link
             1 Watcher.Pause link  ⍝ TODO : WriteFiles/FixFiles unnecessary when un-pausing crawler
         :Else
             U.Error'Not linked: ',ns
         :EndIf
     :EndHold
 :Else
     U.Resignal 1
 :EndTrap
