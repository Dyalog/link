 {names}←{where} Fix src;RSI;XSI;callback;delete;error;file;fix;fixedname;link;links;name;nc;nosrc;ns;nsname;nsref;oldfile;oldname;this;tie;update;⎕IO;⎕ML;add
⍝ Fix a function/operator or script, preserving any existing source files if in a linked namespace
⍝   Used internally by EditorFix "afterfix" processing
⍝   May be called by other tools providing the source in ⎕NR/⎕SRC format on the right
⍝ Return the list of names of objects that were fixed
⍝   NB: in a linked namespace, only single-name sources are supported
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     names←0⍴⊂''
     XSI←⎕XSI ⋄ RSI←⎕RSI
     :If 900⌶⍬ ⋄ where←'' ⋄ :EndIf  ⍝ fix into calling space by default

     nosrc←0=≢src
     :If 9≠⎕NC'where' ⋄ where←⊆,where ⋄ :EndIf  ⍝ do not ravel reference
     (ns name oldname)←3↑where,'' '' ''
     :If 0=≢ns ⋄ ns←⊃RSI ⋄ :EndIf  ⍝ namespace not specified - do not ravel reference
     oldname,←(0=≢oldname)/name ⍝ oldname not specified
     name,←(0=≢name)/oldname ⍝ name not specified
     this←⊃RSI XSI U.ContainerNs ns
     nsname←⍕nsref←this(0 U.GetRefTo)ns
     :If 0∊⍴nsref ⋄ U.Error'Not a namespace: ',ns ⋄ :EndIf

     callback←'⎕SE.Link.OnAfterFix' '⎕SE.SALTUtils.EditorFix'∊⍨⊂⊃1↓XSI  ⍝ v18.0 started using ⎕SE.Link.OnFix/⎕SE.Link.OnAfterFix - v17.1 was called directly from SALT
     add←'⎕SE.Link.Add'≡⊃1↓XSI
     →callback/NOHOLD ⍝ Can't hold in a high-priority callback (Editor AfterFix callback) - see mantis 18160: can't :Hold 0⍴⊂'' in a high-priority callback
     :Hold '⎕SE.Link.Notify' '⎕SE.Link.Links'
     ⍝ Ok not to hold ⎕SE.Link.Links in high-priority callback because it only reads it and doesn't change it, and no other APL thread will run.
     ⍝ However not holding '⎕SE.Link.Notify' is annoying because there is a non-zero probability of the files being in an incomplete/misleading state
NOHOLD:
         :If 0∊⍴links←U.GetLinks
         :OrIf 0∊⍴link←links U.LookupRef ns←nsref
             :GoTo NOLINK
         :EndIf
         :If ~U.IsNamedRef nsref ⋄ U.Error'Not a properly named namespace: ',ns ⋄ :EndIf
         U.ErrorIfRestored links  ⍝ now that we know we're dealing with a linked namespace, allow error

         (file oldfile nc)←link(0 U.DetermineFileName)nsname name oldname src

         :If nc=¯1 ⋄ U.Error'Invalid name: ',nsname,'.',name
         :ElseIf (0=nc)∧(~nosrc) ⋄ U.Error'Invalid source for ',nsname,'.',name ⍝ ,': ',⍕src
         :ElseIf (0=nc) ⋄ U.Error'No source for ',nsname,'.',name
         :ElseIf callback∧(2.1=nc)∧~⎕NEXISTS file  ⍝ only update array if file already exists
             ¯1 U.Log'ignoring editing of array: 'nsname'.'name
             :Return
         :EndIf

         update←1  ⍝ explicit call always updates file
         :If callback
             update←(⊂link.watch)∊'ns' 'both'   ⍝ callback must watch ns
             ⍝update>←(2.1=nc)∧(~⎕NEXISTS file)   ⍝ never create file on new array unless was explicitly added
         :EndIf
         tie←U.FixTie link  ⍝ should we tie files when fixing

         delete←(0≠≢oldfile)∧(file≢oldfile)∧(name≡oldname)  ⍝ old file must be removed : file name was changed by beforeWrite or forceFilename or forceExtensions
         :If callback∧(0≠≢oldfile)∧(oldname≢name)  ⍝ Repair damage done by editor on rename - won't happen if oldname is undefined (link issue #190) because oldfile must then be empty
             :If (1↓U.LASTFIX)≢(⍕ns)(oldname) ⋄ U.Error'Fixing error: last fix is ',(⍕2⊃U.LASTFIX),'.',(3⊃U.LASTFIX),' instead of ',(⍕ns),'.',(oldname) ⋄ :EndIf
             ns U.Untie name  ⍝ editor ties newname to oldfile
             (⊃U.LASTFIX)U.Into oldfile   ⍝ editor overwrites oldfile with new definition
             :If 0>ns U.QFix oldfile oldname tie 1 ⍝ fix must succeed ! FileSystemWatcher will pick up the change but won't fix it since it's already fixed - TODO it means it will re-hash the file
                 U.Error'Could not fix ',(⍕ns),'.',oldname,' from "',oldfile,'"'
             :EndIf
         :ElseIf callback∧(~tie)∧(0=≢oldfile)∧(oldname≢name)   ⍝ untied rename
         :AndIf (ns.⎕NC oldname)∊3 4 ⍝ workaround Mantis 18992
             :If oldname≢⊃ns U.Fix oldname(ns.⎕NR oldname)1  ⍝ repair damage done by editor - 0 ⎕ATX may hold an incorrect version, compared to ⎕NR
                 U.Error'Could not fix ',(⍕ns),'.',oldname
             :EndIf
         :ElseIf callback∧(0=≢oldfile)∧(oldname≡name)  ⍝ new name - link issue #196 v18.0 needs to preserve source as typed
             :If (1↓U.LASTFIX)≢(⍕ns)(oldname) ⋄ U.Error'Fixing error: last fix is ',(⍕2⊃U.LASTFIX),'.',(3⊃U.LASTFIX),' instead of ',(⍕ns),'.',(oldname) ⋄ :EndIf
             :If 2=ns.⎕NC name ⋄ src←⎕SE.Dyalog.Array.Serialise ns⍎name  ⍝ source of array
             ⍝:ElseIf ~tie ⋄ :AndIf (ns.⎕NC name)∊3 4 ⍝ functions not tied to file cannot preserve whitespace - Link.Diff would otherwise report a diff between file and namespace
             ⍝    src←ns.⎕NR name    ⍝ use ⎕NR so that workspace matches file
             :Else ⋄ src←⊃U.LASTFIX  ⍝ source as typed
             :EndIf
             nosrc←0
         :Else
         :EndIf

         :If callback   ⍝ called back from editor
             U.LASTFIX←⍬  ⍝ last fix event consumed
             :If ~update ⍝ Editor fix when not watching the namespace
                 :Return  ⍝ job done
             :EndIf
         :EndIf

         :If nosrc    ⍝ Source not provided - find it
             :If 1=≡src←ns U.GetAplSource name  ⍝ string source means error
                 U.Error src  ⍝ failed to get source
             :ElseIf nc=2.1  ⍝ link issue #205 - check round-trip of serialisation
             :AndIf (ns⍎name){0::1 ⋄ ⎕CT←0 ⋄ ⍺≢⎕SE.Dyalog.Array.Deserialise ⍵}src
                 U.Error'Cannot round-trip serialisation of array: ',nsname,'.',name
             :EndIf
         :EndIf

         :If 3=link.(⎕NC beforeWrite)  ⍝ user callback on file write
             :If ~(⍎link.beforeWrite)'beforeWrite'link file(nsname,'.',name)(|nc)(nsname,'.',oldname)src
                 ¯1 U.Log'name='nsname'.'name' file="'file'" handled by user'
                 :Return
             :EndIf
         :EndIf

         :If nc≠¯9.1 ⋄ :AndIf U.HasNewlines∊src
             U.Error'Source code cannot have newlines'
         :EndIf

         fix←~nosrc ⍝ no need to update if source was read from APL
         fix∧←~callback ⍝ no need to refix if change was done through editor (in which case src was empty anyways)
         ¯1 U.Log'name='nsname'.'name' file="'file'" fix='(⍕fix)' update='(⍕update)' tie='(⍕tie)' oldname='oldname' delete='(⍕delete)' oldfile="'oldfile'"'

         error←''
         :If update  ⍝ file system must reflect changes - FileWatcher may pick it up after this high-priority callback, and should notice no change in source, therefore not fix it  - TODO it means it will re-hash the file
             :Trap 0
                 :If nc=¯9.1  ⍝ tradns → folder
                     3 ⎕MKDIR file
                 :Else  ⍝ file
                     3 ⎕MKDIR 1⊃⎕NPARTS file ⍝ make sure folder is there
                     src U.Into file
                 :EndIf
             :Else  ⍝ do not error yet - we still want to attempt to fix the source in the workspace
                 error←'Unable to write ',nsname,'.',name,' to file: ',file
             :EndTrap
             :If delete  ⍝ old file must be removed
                 ⎕NDELETE oldfile
             :EndIf
         :EndIf

         :If fix
             :If (0∊⍴error)∧(update∧tie) ⋄ src←file ⋄ :EndIf  ⍝ unless there was an error updating the file
             (fixedname nc)←ns U.Fix name src 1   ⍝ do not call QFix because file may be out-of-sync
             :If nc∊¯1 0
                 U.Error'Unable to fix ',nsname,'.',name,' from source: ',⍕src
             :EndIf
             names←,⊂fixedname
         :Else
             names←,⊂name
         :EndIf

         :If ~0∊⍴error ⋄ U.Error error ⋄ :EndIf

     :EndHold ⍝ Links
     :Return

NOLINK:
     :If ~callback  ⍝ leave editor alone if not linked
     :AndIf ~add    ⍝ Link.Add fixes only in linked namespaces
         names←2 nsref.⎕FIX src
     :EndIf

 :Else
     U.Resignal 1
 :EndTrap
