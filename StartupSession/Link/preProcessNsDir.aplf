 r←{ucmd}preProcessNsDir args;rsi;xsi;first;container;ns;dir;nsgiven
⍝ Preprocess arguments to Create, Import and Export

 (container ns)←(rsi xsi←1↓¨⎕RSI ⎕XSI)U.ContainerNs''
 first←⊃args←,⊆args ⋄ dir←''

 nsgiven←2=≢args
 :Select ≢args
 :Case 1
     dir←⊃args
     ns←' '~⍨2⊃⎕NPARTS dir
 :Case 2
     (ns dir)←args
 :Else
     U.Error'Argument should be a path name, optionally preceded by a namespace name'
 :EndSelect

 (container ns)←(rsi xsi)U.ContainerNs ns

 :If ~∨/':/\'∊⍕dir ⍝ not a file/directory name, e.g. ]link.create HttpCommand
     dir←resolveSimpleName dir
 :EndIf
 r←container ns dir nsgiven ⍝ /// SYNTAX ERROR in v19.0 if (container ns dir nsgiven)← in function header
