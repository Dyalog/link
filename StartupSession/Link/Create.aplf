 msg←{opts}Create(ns dir);arrow;container;emptydir;emptyns;fail;hasdir;hasns;nc;nsref;nsrefs;overwrite;rawns;rsi;warn;xsi;⎕IO;⎕ML
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :Hold '⎕SE.Link.Links'   ⍝ functions that use ⎕SE.Link.Links must :Hold '⎕SE.Link.Links' to avoid e.g. wrong results in lookup
         {}U.HasLinks                ⍝ Clean up links if necessary
         :If 0=⎕NC'⎕SE.Link.Links'   ⍝ init links if necessary
             ⎕SE.Link.Links←⍬
         :EndIf

         msg←⍕⍬⊤⍬ ⋄ rawns←,⍕ns
         (container ns)←(rsi xsi←⎕RSI ⎕XSI)U.ContainerNs ns

         opts←⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?
         :If warn←~Watcher.CanWatch  ⍝ warn if not watching directory by default - will disappear when file crawler works
             :If ''≡0⍴opts ⋄ opts←⎕SE.Dyalog.Array.Deserialise opts ⍝ pseudo array notation (experimental)
             :Else ⋄ opts←⎕SE.Link.⎕NS opts  ⍝ duplicate namespace to avoid changing caller's, and to avoid having cross-refs between # and ⎕SE
             :EndIf
             :If warn←0=opts.⎕NC⊂'watch' ⋄ :AndIf warn←~Watcher.CanWatch
                 opts.watch←'ns' ⍝ default to watch='ns' and warn about it
             :EndIf  ⍝ otherwise default to 'both'
         :EndIf
         opts←U.DefaultOpts opts
         :If ~(⊂opts.arrays)∊0 1 ⋄ opts.arrays←container U.NormName¨opts.arrays ⋄ :EndIf


         :If hasdir←⎕NEXISTS dir ⋄ emptydir←0∊⍴⊃(⎕NINFO⍠1)dir,'/*'
         :Else ⋄ emptydir←1
         :EndIf

         :If hasns←¯9.1=nc←U.NameClass ns
             :If ~emptyns←0∊⍴U.ListNames nsref←⍎ns  ⍝ some APL names defined
             :AndIf ~emptydir                       ⍝ some dir/files defined
             :AndIf (⊂opts.source)∊'auto' 'dir'     ⍝ will not erase dir
             ⍝ link issue #160: allow some APL names to be tied to source files when source is dir - typically happens when starting dyalog with load=boot.aplf - won't work with arrays
                 emptyns←emptyns U.EmptyNamespace nsref dir
             :EndIf
         :Else ⋄ emptyns←1
         :EndIf

         :If ~Watcher.CanWatch ⋄ :AndIf (⊂opts.watch)∊'both' 'dir' ⋄ :AndIf ⎕SE.Link.Test≢⊃⎕RSI  ⍝ ⎕SE.Link.Test is allowed to toy with file watching/crawling
             U.Error'Watching directories requires Dyalog v18.0 and either .NET or .NetCore'
         :ElseIf ¯1=nc  ⍝ typically when doing (⎕SE.Link.Create dir ns) instead of (⎕SE.Link.Create ns dir)
             U.Error'Mal-formed namespace name: ',rawns
         :ElseIf ~hasdir ⋄ :AndIf opts.source≡'dir'
             U.Error'Source directory not found: ',dir
         :ElseIf ~hasns ⋄ :AndIf opts.source≡'ns'
             U.Error'Source namespace not found: ',ns
         :ElseIf ~emptyns ⋄ :AndIf opts.source≡'dir'
             U.Error'Destination namespace not empty: ',ns
         :ElseIf ~emptydir ⋄ :AndIf opts.source≡'ns'
             U.Error'Destination directory not empty: ',dir
         :ElseIf emptydir⍱emptyns ⋄ :AndIf opts.source≡'auto'
             U.Error'Cannot link a non-empty namespace to a non-empty directory: ',ns,' ←→ ',dir
         :ElseIf hasdir⍱hasns ⋄ :AndIf opts.source≡'auto'   ⍝ MK @ link issue #136 Q2 : would create a lot of dummy directories/namespaces on typos
             U.Error'Cannot link a non-existent namespace to a non-existent directory: ',ns,' ←→ ',dir
         :ElseIf 0=≢nsref←1 U.GetRefTo ns  ⍝ attempt to create namespace if it doesn't exist
             :If ~U.IsNamedNs ns  ⍝ link issue #197 - ns is ref to unnamed namespace
                 U.Error'Not a properly named namespace: ',ns
             :Else
                 U.Error'Not a namespace: ',ns
             :EndIf
         :ElseIf ~U.IsNamedRef nsref  ⍝ ns is name of unnamed namespace
             U.Error'Not a properly named namespace: ',ns
         :ElseIf U.IsScripted nsref
             U.Error'Cannot link to scripted namespaces: ',ns
         :ElseIf ×≢⎕SE.Link.Links ⋄ :AndIf nsref∊nsrefs←⍎¨⎕SE.Link.Links.ns
             U.Error'Already linked: ',U.Arrow ⎕SE.Link.Links⊃⍨nsrefs⍳nsref
         :ElseIf ~hasdir ⋄ :AndIf ~3 ⎕MKDIR dir  ⍝ attempt to create directory if it doesn't exist
             U.Error'Directory not found: ',dir
         :ElseIf U.Slash⊃⌽dir
             U.Error'Trailing slash reserved for future extension'
         :EndIf
         :If opts.source≡'auto'  ⍝ detect actual source
             :If hasns<hasdir
             :OrIf (hasns=hasdir)∧(emptydir≤emptyns)  ⍝ if have both, see which is not empty, if both empty, dir wins
                 opts.source←'dir'                    ⍝ will also happen if neither exist
             :Else   ⍝ (hasns>hasdir)∨(hasns=hasdir)∧(emptydir>emptyns)
                 opts.source←'ns'
             :EndIf
         :EndIf
         :If warn ⋄ ¯1 U.Warn'.NET or .NetCore not available - Link cannot watch directory' ⋄ :EndIf

         ⍝ We're all good; do it!
         opts.ns←ns←U.NormNs ns
         opts.dir←dir←0 U.NormDir dir
         msg←0⍴⊂''
         :Select opts.source
         :Case 'ns'
             :If 0≢opts.flatten ⋄ U.Error'flatten modifier requires source≡''dir''' ⋄ :EndIf
             :If 0≢opts.fastLoad ⋄ U.Warn'fastLoad modifier ignored when source≡''ns''' ⋄ :EndIf
             overwrite←1 ⍝ we already checked the overwrite condition
             fail←2⊃opts U.WriteFiles ns dir opts.arrays opts.sysVars overwrite
             :If 0≠≢fail ⋄ msg,←(⊂(⍕≢fail),' export(s) failed:'),fail ⋄ :EndIf
         :Case 'dir'
             :If 0≢opts.arrays ⋄ U.Warn'arrays modifier ignored when source≡''dir''' ⋄ :EndIf
             :If 0≢opts.sysVars ⋄ U.Warn'sysVars modifier ignored when source≡''dir''' ⋄ :EndIf
             fail←2⊃opts U.FixFiles nsref dir 1 ⍝ we already checked the overwrite condition
             :If 0≠≢fail ⋄ msg,←(⊂(⍕≢fail),' import(s) failed:'),U.WinSlash¨fail ⋄ :EndIf
         :Else
             U.Error'Unknown source setting:'opts.source
         :EndSelect
         :If 'dir' 'both'∊⍨⊂opts.watch
             Watcher.Watch opts
         :EndIf
         arrow←U.Arrow opts
         opts.⎕DF 1⌽'][',arrow
         ⎕SE.Link.Links,←opts
         msg←1↓U.FmtLines(⊂'Linked: ',arrow),msg
     :EndHold
 :Else
     U.Resignal 1
 :EndTrap
