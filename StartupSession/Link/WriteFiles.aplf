 outFail←{opts}WriteFiles(source dir)
 ⍝ Write items to file in folders
 ;nc;okFn;okNs;nss;unscripted;failed;paths;ok;fns
 ;FileName;SaveFn;SaveNs
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0

     opts←DefaultOpts⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?
     :If 0=opts.⎕NC'dir'
         opts.dir←0 U.NormDir dir
     :EndIf

     FileName←{ ⍝ filename ← src FileName name
         ⊃0 U.DetermineFileName opts source ⍵ ⍵ ⍺
     }
     SaveNs←{ ⍝ Save namespace whether scripted or not
         0=≡⍵:0                ⍝ prototype means
         16::0 ⍵               ⍝ problem: no script
         src←⎕SRC source.⍎⍵    ⍝ get source from its location
         22::⍵ 0               ⍝ failed: no file access
         0 0⊣src U.Into src FileName ⍵ ⍝ save as namespace and report OK
     }
     SaveFn←{  ⍝ Save function there
         nr←source.⎕NR ⍵
         nr U.Into nr FileName ⍵
     }
     nc←{⎕NC⊂,'⍵'}source
     okFn←3.1 3.2 4.1 4.2 ⍝ tradfn/dfn/tradop/dop
     okNs←9.1 9.4 9.5 ⍝ ns/class/interface
     :If nc=0 ⍝ undef
     :ElseIf nc∊okFn
         source←source↑⍨1-'.'⍳⍨⌽source ⍝ Item name without ns path
         outFail←0⍴⊂0⍴SaveFn source ⍝ single fn/op
     :ElseIf (nc∊okNs)∨(nc∊9.2)∧source≡# ⍝ # is reported as an instance (9.2)
         failed←0⍴⊂''
         :If ~0∊⍴fns←source.⎕NL-okFn                        ⍝ tradfn, dfn, tradop, dop
             failed,←0~¨⍨SaveFn U.OnEach fns                ⍝ try to save the functions
         :EndIf
         :If 0∊⍴nss←source.⎕NL-okNs
             failed←unscripted←0⍴⊂''
         :Else
             (failed unscripted)←0~¨⍨↓⍉↑SaveNs U.OnEach nss ⍝ go through them
             paths←''∘FileName U.OnEach unscripted          ⍝ needed dirs
             paths←{∊2↑⎕NPARTS ⍵} U.OnEach paths            ⍝ Remove .apln extensions from folder names
             ok←{⎕NEXISTS ⍵⊣3 ⎕MKDIR ⍵}U.OnEach paths       ⍝ Try and make sure dir exists
             failed,←paths/⍨~ok                             ⍝ also remember failed paths for nss
             paths/⍨←ok                                     ⍝ the good paths
             unscripted/⍨←ok                                ⍝ and their namespaces
             failed,←⊃,/unscripted{opts WriteFiles(source.⍎⍺)⍵}U.OnEach paths
         :EndIf
         outFail←failed
     :Else ⍝ invalid type
         outFail←,⊂source
     :EndIf
     outFail(⍕¨~)←⊂⍬
 :Else
     U.Resignal 1
 :EndTrap
