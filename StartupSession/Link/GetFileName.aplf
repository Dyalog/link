 files←GetFileName items;container;file;i;link;links;name;nc;oldfile;parent;scal;shape;⎕IO;⎕ML
 ⍝ Get file names from APL names
 ⍝ returns empty string for unlinked names
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬
     :Hold '⎕SE.Link.Notify' '⎕SE.Link.Links'
         :If 0∊⍴links←U.GetLinks ⋄ files←⊃⍣(1=≡,items)⊢(⍴⊆items)⍴⊂'' ⋄ :Return ⋄ :EndIf
         scal←1=≡,items ⋄ shape←⍴items←⊆items ⋄ files←(≢items←,items)⍴''
         :For i :In ⍳≢items
             nc←container U.NameClass name←(⍕container),'.',i⊃items
             (parent name)←U.SplitNs name
             :If ~nc∊0 ¯1
             :AndIf ~0∊⍴link←links U.LookupRef parent←⍎parent
                 (file oldfile nc)←link(0 U.DetermineFileName)(⍕parent)name name ⍬ ⍬ ⍝ U.NormNs parent
                 (i⊃files)←(~nc∊0 ¯1)/(1+0<≢oldfile)⊃file oldfile
             :Else
                 (i⊃files)←''
             :EndIf
         :EndFor
         files←⊃⍣scal⊢shape⍴files
     :EndHold
 :Else
     U.Resignal 1
 :EndTrap
