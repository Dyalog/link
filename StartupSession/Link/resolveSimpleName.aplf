 r←resolveSimpleName name;path;libs;lib;files;del;paths
⍝ Try to find something like "HttpCommand"

 r←name ⍝ If we cannot improve it, return argument unchanged
 libs←'[DYALOG]/Library/*'
 :If 0≠≢path←2 ⎕NQ'.' 'GetEnvironment' 'DYALOGCODEPATH'
     libs←U.PATHDEL(≠⊆⊢),(libs/⍨U.PATHDEL=⊃path),path
 :EndIf

 :For lib :In libs
     lib←⎕SE.Dyalog.Utils.ExpandConfig lib
     paths←⊃0 ⎕NINFO⍠1⊢lib
     :For path :In paths
         :Trap 22
             :If 0≠≢files←⊃0 ⎕NINFO⍠1⊢path,'/',name,'*'
             :AndIf 0≠≢files/⍨←(3⊃¨⎕NPARTS files)∊'.',¨U.CODE_EXTENSIONS
                 →0⊣r←(⊃⍋⎕C files)⊃files
             :EndIf
         :EndTrap
     :EndFor
 :EndFor
