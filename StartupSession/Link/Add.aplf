 msg←{api}Add items;added;item;name;ns;unfound;unlinked;⎕IO;⎕ML;setconfig;opts;lines;z;type;⎕TRAP;unnamed
 ⎕IO ⎕ML←1 1
 preProcessOpts ⍝ Make sure opts is a namespace
 (msg setconfig)←Config.GetConfig opts
 →(0≠≢msg)⍴0
 :If 2=DEBUG←{6::0 ⋄ ⍵.Debug.debug}setconfig
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0

     items←,items
     :If '←'∊items
         ns←⊃⌽⎕RSI ⎕XSI U.ContainerNs''
         ns⍎items
         items←' '@(∊∘'()←')items↑⍨items⍳'←'  ⍝ extract names from (possibly nested) name list
     :EndIf
     items←' '(≠⊆⊢)⍣(1=≡,items)⊢items
     items←{⊃⌽⍵}¨⎕RSI ⎕XSI∘U.ContainerNs¨items  ⍝ fully qualified name
     added←unlinked←unfound←unnamed←0⍴⊂''
     :For item :In items
         :If ~(# U.NameClass item)∊0 ¯1
             (ns name)←U.SplitNs item
             :If (⎕NC item)∊3 4
                 z←Stop item(⎕STOP item)
                 z←Trace item(⎕TRACE item)
             :EndIf
             :If ~U.IsNamedRef ns⍎'⎕THIS'
                 unnamed,←⊂ns
             :ElseIf ~0∊⍴ns name Fix''  ⍝ was linked and fixed
                 added,←⊂item
                 :If 9(0⍴⊂'')≡40 62 ⎕ATX item ⍝ unscripted namespace
                     :If U.IsNamedNs item
                         (added unlinked unfound unnamed),←1 Add(item,'.')∘,¨(⍎item).⎕NL-⍳9
                     :Else
                         unnamed,←⊂item
                     :EndIf
                 :EndIf
             :Else
                 unlinked,←⊂item
             :EndIf
         :Else
             unfound,←⊂item
         :EndIf
     :EndFor
     :If 900⌶⍬
     :OrIf 1≢api ⍝ can be an opts ns
         msg←0⍴⊂''
         :If ×≢added ⋄ msg,←⊂'Added:',∊' ',¨added ⋄ :EndIf
         :If ×≢unlinked ⋄ msg,←⊂'Not in a linked namespace:',∊' ',¨unlinked ⋄ :EndIf
         :If ×≢unfound ⋄ msg,←⊂'Not found:',∊' ',¨unfound ⋄ :EndIf
         :If ×≢unnamed ⋄ msg,←⊂'Not properly named:',∊' ',¨unnamed ⋄ :EndIf
         msg←1↓U.FmtLines msg
     :Else
         msg←added unlinked unfound unnamed
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
