 {ok}←WSLoaded;keep;links;msg;wslinks;link;diff;allOK;load;IsLoad;broken;isLoadWs;preload
⍝ Action to be taken upon )LOAD or )CLEAR
 ok←0

 :Trap 0 ⍝ can't afford to error
     (load preload)←{2 ⎕NQ #'GetEnvironment' ⍵}¨'LOAD' 'PRELOAD'
     IsLoad←load∘{⍵.dir∊⍥(1 ⎕NPARTS⊆)⍺}

     :Hold '⎕SE.Link.Links'

         ⍝ retrieve saved workspace links before the GetLinks which may overwrite Store 0
         :If ~U.HASSTORE
         :OrIf ⎕NULL≡wslinks←U.Store 0
             wslinks←⍬
         :EndIf

         :If ok←(∨/preload∊'1yY')∧load≡⍥{⎕C ∊¯1↓1 ⎕NPARTS ⍵}⎕WSID
             U.SetLinks ⍬ ⍝ PRELOADED=Y means application intends to recreate links with preloaded:1
             →0           ⍝ Exit if we are in a preloaded workspace
         :EndIf

         ⍝ preserve old links to ⎕SE - drop the rest
         :If ~0∊⍴links←U.GetLinks
         ⍝ keep links in ⎕SE
         :AndIf 0∊keep←(⎕SE=U.RootOf⍎¨links.ns)∨(~U.IS190)∧(IsLoad links)∧~2∊⎕NC'⎕SE.Dyalog.Callbacks.loaded'
             ¯1 U.Log'Clearing # links:'(⍕(~keep)/links)
             Watcher.Break(~keep)/links
             links←keep/links
         :Else
             :If 0≠≢links
             :AndIf 0≠≢broken←((⍕¨links.fsw)∊⊂'(NULL)')/links
                 ⍝ /// NAFF!!! But this is what happens to FSW's on )CLEAR ///
                 broken.fsw←Watcher.Watch¨broken
             :EndIf
             ¯1 U.Log'Nothing to clear'
         :EndIf

         ⍝ restore saved workspace links
         :If ~0∊⍴wslinks
             wslinks←⎕SE.Link.⎕NS¨wslinks  ⍝ avoid cross-ref between # and ⎕SE
             :If U.IS190
                 isLoadWS←(,1)≢IsLoad wslinks  ⍝ wslinks is not exactly the LOAD= directory
                 wslinks/⍨←isLoadWS            ⍝ before 19.0, always restore saved links
             :EndIf
         :AndIf ~0∊⍴wslinks
             links,←wslinks
             wslinks.restored←1 ⋄ wslinks.fsw←⎕NULL  ⍝ fsw re-instancited by Resync
         :EndIf

         U.SetLinks links
     :EndHold

     allOK←0
     :Trap 0 ⍝ If diffs fail for any reason, bail out
         diff←0 4⍴0
         :For link :In wslinks
             diff⍪←⎕SE.Link.Diff link.ns
         :EndFor
         allOK←0=≢diff
     :EndTrap

     :If allOK
         :If 0<≢links
             links.restored←0 ⍝ No resync required
         :EndIf
         :If 1=≢wslinks                    ⍝ LOAD might provide 1 link:
         :AndIf ''≢load                    ⍝ must be set
         :AndIf IsLoad⊃⎕SE.Link.Links      ⍝ must match the requested dir when normalised
         :AndIf (,'#')≡⊃⎕SE.Link.Links.ns  ⍝ must go to hash
             ¯1 U.Log'Ignoring existing link to # caused by LOAD=',load
         :ElseIf 0≠≢wslinks
             msg←(⍕≢wslinks),' link',('s '/⍨1≠≢wslinks),' restored: '
             U.Warn(⊂msg),(⍕¨wslinks.ns)
         :EndIf
     :ElseIf 0≠≢wslinks
         msg←(⍕≢wslinks),' namespaces',('s '/⍨1≠≢wslinks),' linked in this workspace: '
         'IMPORTANT'U.Warn(⊂msg),⍕¨wslinks.ns
         'IMPORTANT'U.Warn'Link.Resync is required'
     :Else
         ¯1 U.Log'No links found'
     :EndIf

     ok←1
 :Else
     ⎕←'Link.WSLoaded: Unable to clear links (',⎕DMX.(Message{⍵,⍺,⍨': '/⍨×≢⍺}EM),')'
 :EndTrap
