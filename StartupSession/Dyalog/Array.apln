:Namespace Array   ⍝ A model for a literal notation for most APL arrays
    ⎕IO←1 ⋄ ⎕ML←1
    DEBUG←1

    sysVars←'⎕CT' '⎕DIV' '⎕IO' '⎕ML' '⎕PP' '⎕RL' '⎕RTL' '⎕WX' '⎕USING' '⎕AVU' '⎕DCT' '⎕FR'
    L←{1=⊃⍋18(⊃⌽'.'⎕VFI 2⊃#⎕WG'APLVersion'):⎕C ⍵ ⋄ 819⌶⍵}

    :Namespace serialise
        ⎕FR←1287 ⋄ ⎕PP←34


        :Section UTIL
        (sp qu os cs or cr oc cc ec di nl)←' ''[](){}⊂⋄',⎕UCS 13
          Clean←{
              t←'^'''',' ',''''$'⎕R''⊢⍵
              one←2=+/'()'∊⍨'''[^'']+'''⎕R''⊢t
              strip←one∧'()'≡(⊃,⊢/)t
              (strip↓⌽)⍣2⊢t
          }
          Esc←{
              Sub←{
                  cont←⍵.Lengths[3]
                  nums←⍕⎕UCS ⍵.Match↓⍨-cont
                  ''',',(cont⍴'('),'⎕UCS ',nums,cont/'),''',⊃⌽⍵.Match
              }
              '([\x00-\x1F\x7F-\x9F\x{2028}\x{2029}\x{FEFF}\x{FFF9}-\x{FFFF}]+)(.?)'⎕R Sub⍠'Mode' 'D'⊢⍵
          }
        Unempty←⊢↑⍨1⌈≢
          Empty←{
              ⍵,⍨'0⌿'/⍨Lead0 ⍺
          }
          _Sub_←{
              (o c)←⍵⍵
              inner←∊⍺⍺ Unempty ⍵
              ⍵ Empty o,nl,inner,c
          }
        Join←{⍺←nl ⋄ ¯1↓∊⍵,¨⍺}
          _Paren_←{
              res←⍵⍵ ⍵
              ⍺⍺ res:or,res,cr
              res
          }
        :EndSection
        :Section REPR
          Repr←{
              ⍵≡⍬:'⍬'
              ⍵≡0⍴⎕NULL:'0⌿⎕NULL'
              Char ⍵:Clean Esc qu,(qu ⎕R qu qu⊢⍵),qu
              Num ⍵:⍕⍵
              Null ⍵:'⎕NULL'⍴⍨5×≢⍵
              ~Scal ⍵:⎕SIGNAL 999
              Ns ⍵
          }
        ∇ repr←{caller}Ns ref;name;names;code;ser;ind;xr;nr;nc
          :If 900⌶⍬
              caller←⊃⎕RSI
          :Else
              caller←serialise.FirstNs caller,⊃⎕RSI
          :EndIf
          :Select nc←⎕NC⊂'ref'
          :CaseList 9.1 9.4 9.5
              :Trap 0
                  repr←or,(Join ⎕SRC ref),nl,cr
              :Else
                  repr←or nl
         
                  names←##.sysVars/⍨≢⌿ref ref.##∘.⍎##.sysVars
                  names,←ref.⎕NL-⍳9
         
                  :For name :In names
                      code←Join ref.⎕NR name
         
                      :Select ref.⎕NC⊂name
                      :CaseList 3.1 4.1
                          repr,←name,':(∇',code,nl,'∇)'
                      :CaseList 3.2 4.2
                          repr,←1↓':'@(<\'←'=⊢)code
                      :CaseList 3.3 4.3
                          xr←183 ref.⌶name
                          nr←ref.⎕NR name
                          repr,←name,':',{1↓¯1↓⍵}⍣(⍬≢⍴xr)⊢xr(caller Tacit)nr
                      :Else
                          repr,←name,':',caller Ser ref⍎name
                      :EndSelect
                      repr,←nl
                  :EndFor
         
                  repr,←cr
              :EndTrap
          :Else
              11 ⎕SIGNAL⍨'Cannot represent ','instance' 'unscripted class' 'unscripted interface' 'external class' 'external interface' 'unknown object'⊃⍨9.2 9.4 9.5 9.6 9.7⍳nc
          :EndSelect
        ∇
          Tacit←{
              2 9∊⍨⊂⍺:⍺⍺ Ser ⍵        ⍝ array
              ⍬≢⍴⍺:∊or,(⍺ ∇¨⍵),(']'/⍨'['≡2⊃⍵),cr  ⍝ derv
              (3 1)(4 1)∊⍨⊂⍺,≢⍵:⍵  ⍝ primitive
              0=≢⍵:11 ⎕SIGNAL⍨'Cannot represent external or locked functions'
              code←Join⊆⍵
              '}'=⊃⌽⊃⌽⍵:code       ⍝ dfn
              '(∇',code,nl,'∇)'    ⍝ tradfn
          }
        :EndSection
        :Section TYPE
        Char←0 2∊⍨10|⎕DR
        Num←2|⎕DR
        Null←∧/⎕NULL≡¨⊢  ⍝ can't use ∧.= because = is pervasive on deep arrays
        Ptr←6=10|⎕DR
        Basic←Char∨Num∨Null
        FirstNum←Num¨⊃⍤/⊢
        FirstNs←{9∊⎕NC'⍵'}¨⊃⍤/⊢
        :EndSection
        :Section PROP
        Simple←{1=≡,⍵}
        Scal←⍬≡⍴
        Nested←{1<|≡⍵}
        Vec1←{(,1)≡⍴⍵}
        HiRank←{1<≢⍴⍵}
        Vec←{1=≢⍴⍵}
        Mat←{2=≢⍴⍵}
        String←Simple∧Char∧Vec
        Table←Mat>0∊(String∨Simple∧Scal∧Num∨Null)¨
        Lead0←0=≢
        Any0←0∊⍴
        Col1←{1=⊃⌽⍴⍵}
        :EndSection
          Ser←{
              ⍺←⊃⎕RSI
              (Simple∧Scal)⍵:Repr ⍵
              Scal ⍵:ec,⍺ ∇⊃⍵
         
              Self←⊂nl,⍨∇
         
              (Any0<Num∧Table)⍵:Join os,cs,⍨{(+/∨\' '≠⌽⍵)↑¨↓⍵}⎕FMT ⍵
              (Any0<Table>Col1)⍵:Join os,cs,⍨{(+/∨\' '≠⌽⍵)↑¨↓⍵}0 1↓⊃,/' ',¨↑¨↓⍉(∨/'⎕UCS'⍷'''[^'']+'''⎕R'')_Paren_(⍺∘∇)¨⍵
              HiRank ⍵:Self⍤¯1 _Sub_ os cs⊢⍵
              Nested ⍵:Self¨_Sub_ or cr⊢⍵
         
              Vec1 ⍵:or,(⍺ ∇⊃⍵),di cr
         
              (Basic∨Scal)⍵:Repr ⍵
              ∧/Basic¨⍵:sp Join(6≤≢)_Paren_ Repr¨⍵
         
              Self¨_Sub_ or cr⊢⍵ ⍝ hetero
          }
    :EndNamespace
      Serialise←{
          ⍺←⍬
          num←serialise.FirstNum ⍺,0
          caller←serialise.FirstNs ⍺,⊃⎕RSI
     
          Bad←{(caller code)←⍵
              '+'∊⍺:0
              value←Deserialise caller.{⍺⍺ ⍵}code ⍝ deserialise in caller namespace to have proper ⎕FR
              code≢caller serialise.Ser value  ⍝ serialisation must round-trip
          }
          code←caller serialise.Ser ⍵
          ⍺ Bad caller code:'It would not be possible to deserialise the generated array notation'⎕SIGNAL 16
     
          split←{(+/∨\' '≠⌽⍵)↑¨↓⍵}⎕FMT code
     
          1∊⍺:'''[^'']+''' '([[(]) ⋄ ' ' ⋄ ([])])' '  +'⎕R'&' '\1' ' ⋄\1' ' '⊢3↓∊' ⋄ '∘,¨split
     
          spaces←⌊|num+2×0=num
          code←'''[^'']+'''⎕R''⊢code                   ⍝ remove quoted text first
          fmt←⎕FMT'\[[^][]+\]' '\([^)(]+\)'⎕R''⍣≡code  ⍝ remove bracket contents
          delta←(+/'(['∊⍨fmt)-(+/fmt∊')]')
          ind←+\0(⌊+0,¯1↓⌈)delta
          ind{⍵,⍨⍺⍴''}¨split
      }



      Deserialise←{ ⍝ Convert text to array
          ⍺←⍬ ⍝ 1=execute expression; 0=return expression
          execute←serialise.FirstNum ⍺,1
          caller←serialise.FirstNs ⍺,⊃⎕RSI

          q←''''
          ⎕IO←0
          SEP←'⋄',⎕UCS 10 13
     
          Unquot←{(⍺⍺ ⍵)×~≠\q=⍵}
          SepMask←∊∘SEP Unquot
          ParenLev←+\(×¯3+7|¯3+'([{)]}'∘⍳)Unquot
     
          Paren←1⌽')(',⊢
          Split←{1↓¨⍺⍺⊂Over(1∘,)⍵}
     
          Over←{(⍵⍵ ⍺)⍺⍺(⍵⍵ ⍵)}
          EachIfAny←{0=≢⍵:⍵ ⋄ ⍺ ⍺⍺¨⍵}
          EachNonempty←{⍺ ⍺⍺ EachIfAny Over((×≢¨⍵~¨' ')/⊢)⍵}
     
          Parse←{
              0=≢⍵:''
              bot←0=⍺
              (2≤≢⍵)>∨/¯1↓bot:⍺ SubParse ⍵
              p←bot×SepMask ⍵
              ∨/p:∊{1=≢⍵:',⊂',⍵ ⋄ ⍵}⍺(Paren ∇)EachNonempty Over(p Split)⍵
              p←2(1,>/∨¯1↓0,</)bot
              ∨/1↓p:∊(p⊂⍺)∇¨p⊂⍵
              ⍵
          }
     
          ErrIfEmpty←{⍵⊣'Array doesn''t have a prototype'⎕SIGNAL 11/⍨(0=≢⍵)}
     
          SubParse←{
              ('})]'⍳⊃⌽⍵)≠('{(['⍳⊃⍵):'Bad bracketing'⎕SIGNAL 2
              (a w)←(1↓¯1∘↓)¨(⍺-1)⍵
              '['=⊃⍵:Paren'{⎕ML←1⋄↑⍵}1/¨',Paren ErrIfEmpty a Parse w ⍝ high-rank
              ':'∊⍵/⍨(1=⍺)×~≠\q=⍵:a Namespace w ⍝ ns
              '('=⊃⍵:Paren{⍵,'⎕NS⍬'/⍨0=≢⍵}a Parse w ⍝ vector/empty ns
              ⍵ ⍝ dfn
          }
     
          SysVar←(L sysVars)∊⍨' '~¨⍨L∘⊆
     
          ParseLine←{
              c←⍵⍳':'
              1≥≢(c↓⍵)~' ':'Missing value'⎕SIGNAL 6
              name←c↑⍵
              (SysVar⍱¯1≠⎕NC)name:'Invalid name'⎕SIGNAL 2
              name(name,'←',⍺ Parse Over((c+1)↓⊢)⍵)
          }
     
          Namespace←{
              p←(0=⍺)×SepMask ⍵
              (names assns)←↓⍉↑⍺ ParseLine EachNonempty Over(p Split)⍵
              quadMask←SysVar names
              quadAssns←'{⍵.(⍵',(∊'⊣',¨quadMask/assns),')}'
              names/⍨←~quadMask
              assns/⍨←~quadMask
              ∊'({'(assns,¨'⋄')quadAssns'⎕NS'('(, '∘,¨q,¨names,¨⊂q')')'}⍬)'
          }
     
          Execute←{   ⍝ overcome LIMIT ERROR on more than 4096 parenthesised expressions
              ExecuteEach←{         ⍝ split at level-1 parentheses and execute each
                  l←(t=¯1)++\t←{1 ¯1 0['()'⍳⍵]}Unquot ⍵ ⍝ parenthesis type and level
                  (h x t l)←(1 0 0 0=⊂∧\l=0)/¨⍵ ⍵ t l   ⍝ extract header before first opening parenthesis
                  ⍺{0::0 ⋄ r←⍺⍎⍵ ⋄ ~(⊃⎕NC'r')∊3 4}h:⍺⍎⍵ ⍝ header must be an functional expression
                  H←⍺{⍺⍺⍎⍵⍵,'⍵'}h                       ⍝ function to apply header to array
                  ' '∨.≠(l=0)/x:⍺⍎⍵                     ⍝ something outside level-1 parentheses - must fall back to ⍎
                  x←(((l>0)∧(l≠1)∨(t=0))×+\(t=1)∧(l=1))⊆x   ⍝ cut expression within level-1 parentheses
                  1=≢x:H ⍺ ∇⊃x                          ⍝ single expression : don't enclose with ¨
                  DEBUG∧1<⌈/l:H ⍺ ∇¨x                    ⍝ force going through the hard code
                  10::H ⍺ ∇¨x ⋄ H ⍺⍎¨x                  ⍝ attempt to ⍎¨ with a single guard - otherwise dig each
              }
              DEBUG:⍺ ExecuteEach ⍵           ⍝ force going through the hard code
              10::⍺ ExecuteEach ⍵ ⋄ ⍺⍎⍵       ⍝ attempt simple ⍎ and catch LIMIT ERROR
          }
     
          ⍝ Make normalised simple vector:
          w←↓⍣(2=≢⍴⍵)⊢⍵                  ⍝ if mat, make nested
          w←{¯1↓∊⍵,¨⎕UCS 13}⍣(2=|≡w)⊢w   ⍝ if nested, make simple
          w←'''[^'']*''' '⍝.*'⎕R'&' ''⊢w ⍝ strip comments
          w/⍨←{(∨\⍵)∧⌽∨\⌽⍵}33≤⎕UCS w     ⍝ strip leading/trailing non-printables
     
          pl←ParenLev w
          (0≠⊢/pl)∨(∨/0>pl):'Unmatched brackets'⎕SIGNAL 2
          ∨/(pl=0)×SepMask w:'Multi-line input'⎕SIGNAL 11
          (⊃⎕RSI)Execute⍣execute⊢pl Parse w    ⍝ materialise namespace as child of calling namespace
      }


      Inline←{
          caller←serialise.FirstNs ⍵,⊃⎕RSI
          aa←⍺⍺
          caller Deserialise 1↓∊(⎕UCS 13),¨'^( *[\w∆⍙]+ *← *)?{( |\R)*|( |\R)*}\R*$'⎕R''⍠'Mode' 'D'⎕NR'aa'
      }


      Is←{
          0::'MISMATCH'⎕SIGNAL 999
          a←Serialise ⍺⍺ Inline 1
          a≡Serialise ⍵:
          a≡Serialise ⎕JSON ⍵:
          ∧/⍵(1∊⍷)¨⊂↑a:
          !#
      }


      Ed←{ ⍝ Skip ⍺⍺ QA if interpreter skips lines that look like invalid ∇ Editor directives
          </⍋(18 1)(⊃⌽'.'⎕VFI 1⊃# ⎕WG'APLVersion'):⍺⍺ ⍵
      }


      ∆NS←{ ⍝ allows ⎕NS names values
          ⍺←⊢ ⍝ default to unnamed namespace
          11::⎕SIGNAL 11
          (0=≢⍵)∨2≥|≡⍵:{_←⍵}⍣(2∊⎕NC'⍺')⊢⍺ ⎕NS ⍵ ⍝ default behaviour
          {_←⍵}⍣(2∊⎕NC'⍺')⊃⊃(⍺⊣⍣(2∊⎕NC'⍺')⊢⍺ ⎕NS ⍬){ ⍝ new behaviour
              (,1)≢(⍴,≡)⍵:⍺⍺⍎⍺,'←⍵ ⋄ ⍺⍺' ⍝ non-⎕OR: use value
              4 11::⍺⍺⍎⍺,'←⎕NS ⍵ ⋄ ⍺⍺' ⍝ object?
              ⍺⍺⍎⍺,'←⍎⎕FX ⍵ ⋄ ⍺⍺⊣⍺{⍺≡⍵:⍬ ⋄ ⎕EX ⍵}⎕FX ⍵' ⍝ function?
          }¨/⍵
      }

      ∆NSinverse←{ ⍝ ⎕NS⍣¯1
          ⍵∘⍎¨'⎕NL-⍳9' '⎕OR¨⎕NL-⍳9' ⍝ names values
      }










    ∇ r←SerialiseQA stop_on_error;Is;⎕FR;⎕IO;⎕TRAP
      ⎕IO←0
      ⎕TRAP←(~stop_on_error)/⊂999 'C' '→r←0'
      Is←{
          0::'MISMATCH'⎕SIGNAL 999
          a←Serialise ⍺⍺ Inline 1
          a≡Serialise ⍵:
          a≡Serialise ⎕JSON ⍵:
          ∧/⍵(1∊⍷)¨⊂↑a:
          !#
      }
     
     
      :Section scalars
          {
              ⎕NULL
          }Is ⎕NULL
          {
              'a'
          }Is'a'
          {
              ''''
          }Is''''
          {
              (⎕UCS 0)
          }Is ⎕UCS 0
          {
              (⎕UCS 13)
          }Is ⎕UCS 13
          {
              (⎕UCS 10)
          }Is ⎕UCS 10
          {
              42
          }Is 42
      :EndSection
      :Section vectors
          {
            (
             '<>'
             ''''
             '"'
             '$'
            )
          }Is (⊂'<>') , '''"$'

          {(42 ⋄ )}Is,42
     
          {(42
              )}Is,42
     
          {
              (1 2 3 'Hello' ⋄ 4 5 6 'World')
          }Is(1 2 3 'Hello')(4 5 6 'World')
     
          {
              (1 2 3 'Hello'
              4 5 6 'World')
          }Is(1 2 3 'Hello')(4 5 6 'World')
     
          {
              (0 1 ⋄ 2 3
              4 5 ⋄ 6 7)
          }Is↓4 2⍴⍳8
     
          {
              ('Three'
              'Blind'
              'Mice')
          }Is 'Three' 'Blind' 'Mice'
      :EndSection
      :Section Matrices
          {
            [
            1 ''
            ¯1 ('-'⋄)
            ]
          }Is 2 2⍴1 '' ¯1 (,'-')

          {[0 1 2
              3 4 5]
          }Is 2 3⍴⍳6

          {[
            (
             'short'
            )
            (
             'longer text'
            )
            (
             'longest test text'
            )]
          }Is ⍪'short' 'longer text' 'longest test text'
     
          {[ ⋄ 0 1 2 3]
          }Is⍉⍪⍳4
     
          {[
              0 1 2 3
              ]
          }Is⍉⍪⍳4
     
          {['Three'
              'Blind'
              'Mice']
          }Is 3 5⍴'ThreeBlindMice '
     
          {[
              ('Three'
              'Blind'
              'Mice')
              ]
          }Is 1 3⍴'Three' 'Blind' 'Mice'
     
          {[1 ⋄ ]
          }Is⍪1
     
          {
              1 2[1 ⋄ ]
          }Is 1 2(⍪1)
      :EndSection
      :Section Combo
          {
              ([0 0 1
              1 0 1
              0 1 1]
     
              [0 1 1
              1 1 0
              0 1 0]
     
              [0 1 1 1
              1 1 1 0]
     
              [0 1 1 0
              1 0 0 1
              0 1 1 0])
     
          }Is Ed{
              _←⍉⍪0 0 1
              _⍪←1 0 1
              _⍪←0 1 1
              r←⊂_
              _←⍉⍪0 1 1
              _⍪←1 1 0
              _⍪←0 1 0
              r,←⊂_
              _←⍉⍪0 1 1 1
              _⍪←1 1 1 0
              r,←⊂_
              _←⍉⍪0 1 1 0
              _⍪←1 0 0 1
              _⍪←0 1 1 0
              r,←⊂_
              r
          }⍬
     
          {[0 'OK' ⋄ 1 'WS FULL' ⋄ 2 'SYNTAX ERROR' ⋄ 3 'INDEX ERROR' ⋄ 4 'RANK ERROR']
          }Is{
              e←⍉⍪0 'OK'
              e⍪←1 'WS FULL'
              e⍪←2 'SYNTAX ERROR'
              e⍪←3 'INDEX ERROR'
              e⍪←4 'RANK ERROR'
              e
          }⍬
          {['a'(⊂1 2)'a'
              (⊂1 2)'a'(⊂1 2)]
          }Is 2 3⍴'a'(⊂1 2)
      :EndSection
     
      :Section High Rank
          {
              [[3
              1 5 9]
              [2 7 1
              2 8]]
          }Is Ed 2 2 3⍴3 0 0 1 5 9 2 7 1 2 8 0
          {
              [
              [
              [
              1 2 3
              4 5 6
              ]
              ]
              ]
          }Is Ed 1 1 2 3⍴1 2 3 4 5 6
          {
              [
              [
              [
              1
              ]
              [
              2
              ]
              ]
              [
              [
              3
              ]
              [
              4
              ]
              ]
              [
              [
              5
              ]
              [
              6
              ]
              ]
              ]
          }Is Ed 3 2 1 1⍴1 2 3 4 5 6
     
      :EndSection
     
      :Section Empty
          {
              ⍬
          }Is ⍬
          {
              (⍬ ⋄ )
          }Is,⊂⍬
          {
              0⍴⊂⍬
          }Is 0⍴⊂⍬
          {
              0⍴⊂⊂⍬
          }Is 0⍴⊂⊂⍬
          {
              0⍴⊂0⍴⊂⍬
          }Is 0⍴⊂0⍴⊂⍬
          {[⍬ ⋄ ]
          }Is⍉⍪⍬
          {
              ⍉[⍬ ⋄ ]
          }Is⍪⍬
          {
              0⍴⊂[⍬ ⋄ ]
          }Is 0⍴⊂⍉⍪⍬
          {
              0⍴⊂⍉[⍬ ⋄ ]
          }Is 0⍴⊂⍪⍬
      :EndSection
     
      :Section Namespace
          {
              ()
          }Is'{}'
     
          {
              (
              )
          }Is'{}'
     
          {
              ()()
          }Is'[{},{}]'
     
          {
              (a:⍳n←3 ⋄ b:n*2)
          }Is'{"a":[0,1,2],"b":9}'
     
          {
              (p:{⍺+⍵}
              m:{⍺-⍵}
              s:,∘(,',')((,',')∘,)
              o:⍣2)
          }Is'p:{⍺+⍵}' 'm:{⍺-⍵}' 's:(,∘(' '))((' ')∘,)' 'o:⍣2'
     
          {
              (v:(1 2 ⋄ 3))
          }Is'{"v":[[1,2],3]}'
     
          {
              (v:(1 2
              3))
          }Is'{"v":[[1,2],3]}'
     
          {
              (v:(1 2 ⋄ 3)
              )
          }Is'{"v":[[1,2],3]}'
     
          {
              (
              ()
              )
          }Is'[{}]'
     
          {(n:())}Is'{"n":{}}'
     
      :EndSection
      r←1
    ∇


    ∇ r←DeserialiseQA stop_on_error;⎕IO;Is;⎕TRAP
      ⎕IO←0
      ⎕TRAP←(~stop_on_error)/⊂999 'C' '→r←0'
      Is←{
          Check←'MISMATCH'⎕SIGNAL 999/⍨⍵∘≢
          a←⍺⍺ Inline 1
          ×⎕NC'⍺':Check a.⎕NR ⍺
          Check{⎕JSON⍣(326∊⎕DR¨∊⍵)⊢⍵}⍣(⍵≢⎕NULL)⊢a
      }
     
      :Section scalars
          {
              'a'
          }Is'a'
          {
              42
          }Is 42
          {
              ⎕NULL
          }Is ⎕NULL
          {
              ⎕UCS 0
          }Is ⎕UCS 0
      :EndSection
      :Section vectors
          {(42 ⋄ )}Is,42
     
          {(42
              )}Is,42
     
          {'Hello',⎕AV}Is'Hello',⎕AV
     
          {
              (1 2 3 'Hello' ⋄ 4 5 6 'World')
          }Is(1 2 3 'Hello')(4 5 6 'World')
     
          {
              (1 2 3 'Hello'
              4 5 6 'World')
          }Is(1 2 3 'Hello')(4 5 6 'World')
     
          {
              (0 1 ⋄ 2 3
              4 5 ⋄ 6 7)
          }Is↓4 2⍴⍳8
     
          {
              ('Three'
              'Blind'
              'Mice')
          }Is'Three' 'Blind' 'Mice'
      :EndSection
      :Section Matrices
          {[0 1 2
              3 4 5]
          }Is 2 3⍴⍳6
     
          {[ ⋄ 0 1 2 3]
          }Is⍉⍪⍳4
     
          {[
              0 1 2 3
              ]
          }Is⍉⍪⍳4
     
          {['Three'
              'Blind'
              'Mice']
          }Is 3 5⍴'ThreeBlindMice '
     
          {[1 ⋄ ]
          }Is⍪1
     
          {
              1 2[1 ⋄ ]
          }Is 1 2(⍪1)
     
          {
              ['[' ⋄ ]
          }Is 1 1⍴'['
          {
              ['(' ⋄ ]
          }Is 1 1⍴'('   ⍝ link issue #271
     
      :EndSection
      :Section Combo
          {
              ([0 0 1
              1 0 1
              0 1 1]
     
              [0 1 1
              1 1 0
              0 1 0]
     
              [0 1 1 1
              1 1 1 0]
     
              [0 1 1 0
              1 0 0 1
              0 1 1 0])
     
          }Is Ed{
              _←⍉⍪0 0 1
              _⍪←1 0 1
              _⍪←0 1 1
              r←⊂_
              _←⍉⍪0 1 1
              _⍪←1 1 0
              _⍪←0 1 0
              r,←⊂_
              _←⍉⍪0 1 1 1
              _⍪←1 1 1 0
              r,←⊂_
              _←⍉⍪0 1 1 0
              _⍪←1 0 0 1
              _⍪←0 1 1 0
              r,←⊂_
              r
          }⍬
     
          {[0 'OK' ⋄ 1 'WS FULL' ⋄ 2 'SYNTAX ERROR' ⋄ 3 'INDEX ERROR' ⋄ 4 'RANK ERROR']
          }Is{
              e←⍉⍪0 'OK'
              e⍪←1 'WS FULL'
              e⍪←2 'SYNTAX ERROR'
              e⍪←3 'INDEX ERROR'
              e⍪←4 'RANK ERROR'
              e
          }⍬
          {['a'(⊂1 2)'a'
              (⊂1 2)'a'(⊂1 2)]
          }Is 2 3⍴'a'(⊂1 2)
      :EndSection
     
      :Section High Rank
          {
              [[3
              1 5 9]
              [2 7 1
              2 8]]
          }Is Ed 2 2 3⍴3 0 0 1 5 9 2 7 1 2 8 0
      :EndSection
     
      :Section Empty
          {
              ⍬
          }Is ⍬
          {
              (⍬ ⋄ )
          }Is,⊂⍬
          {
              0⍴⊂⍬
          }Is 0⍴⊂⍬
          {
              0⍴⊂⊂⍬
          }Is 0⍴⊂⊂⍬
          {
              0⍴⊂0⍴⊂⍬
          }Is 0⍴⊂0⍴⊂⍬
          {[⍬ ⋄ ]
          }Is⍉⍪⍬
          {
              ⍉[⍬ ⋄ ]
          }Is⍪⍬
          {
              0⍴⊂[⍬ ⋄ ]
          }Is 0⍴⊂⍉⍪⍬
          {
              0⍴⊂⍉[⍬ ⋄ ]
          }Is 0⍴⊂⍪⍬
      :EndSection
     
      :Section Namespace
          {
              ()
          }Is'{}'
     
          {
              (
              )
          }Is'{}'
     
          {
              ()()
          }Is'[{},{}]'
     
          {
              (a:⍳n←3 ⋄ b:n*2)
          }Is'{"a":[0,1,2],"b":9}'
     
          'p' 'm'{
              (p:+
              m:{⍺-⍵})
          }Is¨(,'+')(,⊂' m←{⍺-⍵}')
     
          {
              (v:(1 2 ⋄ 3))
          }Is'{"v":[[1,2],3]}'
     
          {
              (v:(1 2
              3))
          }Is'{"v":[[1,2],3]}'
     
          {
              (v:(1 2 ⋄ 3)
              )
          }Is'{"v":[[1,2],3]}'
     
          {
              (
              ()
              )
          }Is'[{}]'
     
          {(n:())}Is'{"n":{}}'
     
      :EndSection
      r←1
    ∇




    ∇ r←RoundtripQA stop_on_error;Check;Is;array;arrays;mask;ns;shapes;⎕FR;⎕IO;⎕TRAP
      ⎕IO←0
      ⎕TRAP←(~stop_on_error)/⊂999 'C' '→r←0'
      Is←{
          Check←'MISMATCH'⎕SIGNAL 999/⍨≢
          ⍝ identical nss don't match, so we compare the generated notation:
          9∊{⊃⎕NC'⍵'}¨∊⍵:(Serialise ⍵)Check Serialise Deserialise Serialise ⍵
          ⍵ Check Deserialise Serialise ⍵
      }
     
      :Section numbers
          ns←⎕NS ⍬
          :For ns.⎕FR :In 645 1287
              Check←ns.{'MISMATCH'⎕SIGNAL 999/⍨⍵≢##.Deserialise ##.Serialise ⍵}
              Check ns.(123)
              Check ns.(○1)  ⍝ issue #272
              Check ns.(0.1J0.1)
          :EndFor
      :EndSection
     
     
      :Section scalars
          Is'a'
          Is 42
          Is ⎕NULL
          Is ⎕UCS 0
          Is ⎕UCS 13
      :EndSection
      :Section vectors
          Is,42
          ⎕FR←1287 ⋄ Is{(¯1*?⍵⍴2)×(*14000×¯1+2×?⍵⍴0)}10  ⍝ 1287
          ⎕FR←645 ⋄ Is{(¯1*?⍵⍴2)×(*700×¯1+2×?⍵⍴0)}10     ⍝ 645
          Is{⍺+0J1×⍵}⌿{(¯1*?⍵⍴2)×(*700×¯1+2×?⍵⍴0)}2 10   ⍝ 1289
          Is 1=?100⍴2                                    ⍝ 11
          Is?100⍴100                                     ⍝ 83
          Is?100⍴32000                                   ⍝ 163
          Is?100⍴2000000000                              ⍝ 323
          Is'Hello',⎕AV[256?256]
          Is(1 2 3 'Hello')(4 5 6 'World')
          Is(1 2 3 'Hello')(4 5 6 'World')
          Is↓4 2⍴⍳8
          Is'Three' 'Blind' 'Mice'
      :EndSection
      :Section Matrices
          Is 2 3⍴⍳6
          Is⍉⍪⍳4
          Is⍉⍪⍳4
          Is 3 5⍴'ThreeBlindMice '
          Is⍪1
          Is 1 2(⍪1)
      :EndSection
      :Section Combo
          Is{
              _←⍉⍪0 0 1
              _⍪←1 0 1
              _⍪←0 1 1
              r←⊂_
              _←⍉⍪0 1 1
              _⍪←1 1 0
              _⍪←0 1 0
              r,←⊂_
              _←⍉⍪0 1 1 1
              _⍪←1 1 1 0
              r,←⊂_
              _←⍉⍪0 1 1 0
              _⍪←1 0 0 1
              _⍪←0 1 1 0
              r,←⊂_
              r
          }⍬
     
          Is{
              e←⍉⍪0 'OK'
              e⍪←1 'WS FULL'
              e⍪←2 'SYNTAX ERROR'
              e⍪←3 'INDEX ERROR'
              e⍪←4 'RANK ERROR'
              e
          }⍬
          Is 2 3⍴'a'(⊂1 2)
          Is 3 3⍴(,¨'1' '0' '1')
          Is'Hello'('' '⍝World:')
          Is'AB''C',⊂'DE'
          Is'ABC'(,⊂⊂'')
          Is'A'(,⊂⊂'')
      :EndSection
     
      :Section High Rank
          Is 1 1 2 3⍴⍳6     ⍝ issue #254
          Is 3 2 1 1⍴⍳6     ⍝ issue #254
          Is 2 3 4 5⍴⍳120   ⍝ issue #254
          Is⊂[3 4 5]⎕AV[?1 2 3 1 5 6⍴256]      ⍝ issue #254
      :EndSection
     
      :Section Large Arrays                  ⍝ (more than 4096 constants) - issue #255
          Is⊂[2 3]?50 100 2 3⍴100              ⍝ Numeric is straightforward
          Is⊂[3 4 5]⎕AV[?50 10 10 1 2 3⍴256]   ⍝ Text gets functional headers produced by Parse
      :EndSection
     
      :Section Empty
          Is ⍬
          Is,⊂⍬
          Is 0⍴⊂⍬
          Is 0⍴⊂⊂⍬
          Is 0⍴⊂0⍴⊂⍬
          Is⍉⍪⍬
          Is⍪⍬
          Is 0⍴⊂⍉⍪⍬
          Is 0⍴⊂⍪⍬
      :EndSection
     
      :Section Namespace
          Is ⎕JSON'{}'
          Is ⎕JSON'{}'
          Is ⎕JSON'[{},{}]'
          Is ⎕JSON'{"a":[0,1,2],"b":9}'
          Is{
              ⍺←⎕NS ⍬
              ⍺.p←{⍺+⍵}
              ⍺.m←{⍺-⍵}
              ⍺
          }⍬
          Is ⎕JSON'{"v":[[1,2],3]}'
          Is ⎕JSON'{"v":[[1,2],3]}'
          Is ⎕JSON'{"v":[[1,2],3]}'
          Is ⎕JSON'[{}]'
          Is ⎕JSON'{"n":{}}'
      :EndSection
     
      :Section Deep arrays
          arrays←⎕NULL((⎕NS ⍬).(⎕THIS⊣var←(idiom←{⍵[⍋⍵]})(foo←{,⍵})123))(?256⍴0)(⎕AV[256?256])
          shapes←⍬ 0 1 256(0 0)(0 256)(256 0)(16 16)
          shapes,←(0 1 256)(0 256 1)(1 0 256)(1 256 0)(256 1 0)(256 0 1)
          shapes,←(0 0 0 0 0 0)(0 1 2 3 4 5)(5 4 3 2 1 0)(0 0 2 3 0 0)(2 0 0 0 0 3)
          shapes,←(1 2 3 4 5)(5 4 3 2 1)
          mask←~¯1⌽4↑⍤0⊢0∊¨shapes  ⍝ empty arrays with namespaces are not supported (they can't be compared with ≡ anyways)
          arrays←(,mask)/(,shapes∘.⍴arrays)
          :For array :In arrays
              Is array
          :EndFor
          Is arrays
      :EndSection
      r←1
    ∇


    ∇ r←QA
    ⍝ fails under 18.0 because of mantis 18132
      r←SerialiseQA 1
      r∧←DeserialiseQA 1
      r∧←RoundtripQA 1
    ∇

:EndNamespace
